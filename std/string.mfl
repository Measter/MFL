import std::term

struct SplitResult is
    field split bool
    field remainder String
    field prefix String
end

// Splits the given string at the first instance of the given
// character. Remaining string does not include the split char.
proc split_front [
    String
    u8  // Char to split on
] to [
    SplitResult
] is
    memory str is String end
    memory char is u8 end

    // [string, char]
    if over xtrd(length) 0= do
        drop

        false swap dup pack(SplitResult)
    else
        char!
        str!

        0(u64) while 
            dup str xtrd(length) <
            if dup do over str xtrd(pointer)+ @ char@ != and end
        do
            // [counter]
            1+
        end
        // We're either at the end of the string, or found our character.

        // [idx]
        if dup str xtrd(length) = do
            // Reached the end of the string
            drop
            false str@ "" pack(SplitResult)
        else
            // We found the split character!

            // Construct the prefix
            str xtrd(pointer)
            // [idx, ptr]
            over
            // [idx, ptr, len]
            swap pack(String) swap

            // [prefix, idx]
            // Construct the remander.
            str@ unpack
            // [prefix, idx, len, ptr]
            over(2)+ 1+
            rev(3) - 1-
            swap pack(String)
            
            swap true rot(3>1)
            // [flag, remainder, prefix]
            pack(SplitResult)
        end
    end
end

proc is_whitespace [u8] to [bool] is
    dup 32=         // Space
    over 9= or      // Tab
    over 10= or     // Line Feed
    over 12= or     // Form Feed
    over 13= or     // Carriage return
    swap drop
end

proc trim_start [String] to [String] is
    memory str is String end
    str!
    
    0 while dup str xtrd(length) < do
        if str xtrd(pointer)@ is_whitespace do
            str@ unpack
            1+ str insd(pointer)
            1- str insd(length)
        else
            // Can't break loops at the moment, so this will do.
            drop
            str@ return
        end
        1+
    end drop

    str@
end


proc trim_end [String] to [String] is
    memory str is String end
    str!

    if str xtrd(length) 0= do str@ return end

    str xtrd(length) 1- while dup 0> do
        if dup str xtrd(pointer) +@ is_whitespace do
            str xtr(length) 1- swap insd(length)
        else
            // Cant break loops at the moment.
            drop str@ return
        end
        1-
    end drop

    str@
end

proc trim [String] to [String] is 
    trim_start trim_end
end

proc remove_start[String u64] to [String] is
    if over xtrd(length) over< do
        "Cannot trim " std::term::ewrite
        std::term::ewrite_ui
        " from string of length "
        xtr(length) std::term::ewrite_ui_ln
        exit
    end

    swap xtr(length) over(2)- swap ins(length)
    xtr(pointer) rot(3<1)+ swap ins(pointer)
end