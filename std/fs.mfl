import std::io::FD
import std::Result
import std::SimpleResult

const SYS_OPEN [u64] is 0x02 end
const SYS_CLOSE [u64] is 0x03 end
const SYS_FSTAT [u64] is 0x05 end

const FLAG_APPEND [u64] is 0x400 end        // O_APPEND
const FLAG_CREATE [u64] is 0x040 end        // O_CREAT
const FLAG_CREATE_NEW [u64] is 0x080 end    // O_EXCL
const FLAG_TEMP [u64] is 0x40_0000 0x1_0000 or end       // O_TMPFILE | O_DIRECTORY
const FLAG_TRUNC [u64] is 0x200 end         // O_TRUNC

const FLAG_READONLY [u64] is 0x0 end         // Q_RDONLY
const FLAG_WRITEONLY [u64] is 0x1 end        // O_WRONLY
const FLAG_READWRITE [u64] is 0x2 end        // O_RDWR

proc open [
    String  // Null-terminated path to the file
    u64     // Flags for opening
] to [Result(FD)] is
    // The syscall requires a null-terminated string.
    // Check for null termination
    if over xtr(len) 1- std::string::at 0 != do
        drop(2)

        0 pack(FD)
        0 1-
        swap pack(Result(FD))
        return
    end

    SYS_OPEN rot(3>1) 0x1b6 // 0o666
    // [sysid, path, flags, mode]

    rot(3<1) xtrd(data) rot(3>1)
    syscall(4)

    // An error ocurred
    if dup cast(s64) 0< do
        cast(s64) 0 swap - cast(u64)
        0 pack(FD)
    else
        pack(FD) 0 swap
    end

    pack(Result(FD))
end

// Opens a file in read-only mode.
proc open_read [
    String // Null-terminated path
] to [Result(FD)] is
    FLAG_READONLY open
end

// Opens a file in read-write mode, creating the file if it doesn't exist.
proc open_write [
    String  // Null-terminated path
    bool    // Append if it already exists
] to [Result(FD)] is
    FLAG_READWRITE FLAG_CREATE or swap
    if do FLAG_APPEND or end
    open
end

// Opens a file in read-write mode, creating the file if it doesn't exist,
// and truncating the file.
proc create [
    String // Null-terminated path
    bool // Force new file.
] to [Result(FD)] is
    FLAG_READWRITE FLAG_TRUNC or FLAG_CREATE or swap
    if do
        FLAG_CREATE_NEW or
    end

    open
end

proc close [FD] to [SimpleResult] is
    xtrd(fd) SYS_CLOSE swap syscall(2)
    cast(s64) 0 swap - cast(u64) pack(SimpleResult)
end

struct Timespec is
    field tv_sec u64
    field tv_nsec u64
end

struct Stat is
    field st_dev u64
    field st_ino u64
    field st_nlink u64
    field st_mode u32
    field st_uid u32
    field st_gid u32
    field __pad0 u32
    field st_rdev u64
    field st_size u64
    field st_blksize u64
    field st_blocks u64
    field st_atim Timespec
    field st_mtim Timespec
    field st_ctim Timespec
    field __reserved s64[3]
end

proc fstat [FD] to [Result(Stat)] is
    memory buf is Stat end
    xtrd(fd)
    SYS_FSTAT swap buf syscall(3) cast(s64)
    if dup 0< do
        0 swap - cast(u64) buf@
    else
        drop buf@ 0 swap
    end
    pack(Result(Stat))
end