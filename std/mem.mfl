import std::Result
import std::SimpleResult

const PROT_READ [u64] { 0x01 }
const PROT_WRITE [u64] { 0x02 }
const MAP_ANONYMOUS [u64] { 0x20 }
const MAP_PRIVATE [u64] { 0x02 }

const SYS_MMAP [u64] { 0x09 }
const SYS_MUNMAP [u64] { 0x0B }

struct MMap(T) {
    pointer T&,
    length u64,
}

proc lang(alloc) alloc(T) [
    u64 // Number of Ts to allocate for
] to [Result(MMap(T))] {
    dup

    sizeof(T) * // We need the number of bytes to allocate, not the number of Ts.
    SYS_MMAP 0 rot(3<1)
    PROT_READ PROT_WRITE or
    MAP_ANONYMOUS MAP_PRIVATE or
    0 // File descriptor
    0 // File offset
    syscall(7)
    dup cast(s64)
    // [len ptr errcode]

    if dup 0< {
        // Some kind of failure
        0 swap - cast(u64)
        // Drop the len and pointer
        rot(3>1) drop(2)
        // Store a null pointer and 0 length.
        0(u64) cast(T&) 0
        pack(MMap)
        pack(Result) return
    }
    drop 0
    // [len ptr errcode]
    rev(3)
    swap cast(T&) swap
    pack(MMap)
    pack(Result)
}

proc lang(free) free(T) [MMap(T)] to [SimpleResult] {
    unpack
    // [ptr len]
    sizeof(T) *
    SYS_MUNMAP rot(3>1) syscall(3)
    cast(s64) 0 swap - cast(u64)
    pack(SimpleResult)
}

proc fill(T) [
    MMap(T)
    T
] to [] {
    var val { T }
    var store { MMap(T) }
    val!
    store!

    0 while dup store xtrd(length) < {
        val@ store over(2) insd
        1+
    } drop
}

proc copy(T) [
    MMap(T) // Source
    MMap(T) // Destination
] to [] {
    if xtr(length) over(2) xtrd(length) != {
        "Mismatched length in copy - src: " std::term::ewrite
        swap xtrd(length) std::term::ewrite_ui
        " dst: " std::term::ewrite
        xtrd(length) std::term::ewrite_ui_ln
        exit
    }

    var src { MMap(T) }
    var dst { MMap(T) }
    dst! src!

    0 while dup dst xtrd(length) < {
        dup src swap xtrd
        over dst swap insd
        1+
    } drop
}

proc mmap_file [
    std::io::FD
    u64 // Protection flags. Must match the FD.
] to [Result(MMap(u8))] {
    over std::fs::fstat
    if xtr(errno) 0!= {
        rot(3>1) drop(2)
        xtrd(errno)
        0(u64) cast(u8&) // Null pointer
        0                    // length
        pack(MMap)
        pack(Result) return
    }
    xtrd(data)

    xtrd(st_size) 0
    // [FD PROT length addr]
    rev(4)
    //[addr length Prot FD]
    unpack
    MAP_PRIVATE swap 0
    SYS_MMAP rot(7>1)
    // We want to keep the length
    over(4)
    rot(8>1)
    syscall(7)

    dup cast(s64)
    // [length ptr errcode]

    if dup 0< {
        // Some kind of failure
        0 swap - cast(u64)
        // Drop the len and pointer
        rev(3) drop(2)
        // Store a null pointer.
        0(u64) cast(u8&)
        // And a 0 length
        0
        pack(MMap)
    } else {
        drop cast(u8&) swap pack(MMap) 0 swap
    }

    pack(Result) return
}

